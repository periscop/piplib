
dnl /**-------------------------------------------------------------------**
dnl  **                              PIPLIB                               **
dnl  **-------------------------------------------------------------------**
dnl  **                           configure.in                            **
dnl  **-------------------------------------------------------------------**
dnl  **                   First version: august 11th  2001                **
dnl  **-------------------------------------------------------------------**/
dnl
dnl Input file for autoconf to build a configuration shellscript.
dnl To build the configure script from the PipLib's top-level directory, use
dnl autoconf -l autoconf autoconf/configure.in > configure
dnl if it doesn't work (invalid option -l) try -I instead
dnl autoconf -I autoconf autoconf/configure.in > configure


AC_PREREQ(2.13)
AC_INIT(source/piplib.c)
AC_CONFIG_AUX_DIR(autoconf)
VERSION="1.3.6"

TO_BUILD_32="32"
TO_BUILD_64="64"
TO_BUILD_MP="MP"

dnl /**************************************************************************
dnl  *                              Checking                                  *
dnl  **************************************************************************/


dnl Checks for typedefs, structures, and compiler characteristics.
dnl Sylvain Girbal suggested to put this into comment to better fit any
dnl architecture (and particularly Itanium -bah-).
AC_CANONICAL_SYSTEM

dnl Checks for programs.
AC_PROG_CC
AC_PROG_LN_S
AC_PROG_RANLIB
AC_PROG_MAKE_SET
AC_CHECK_PROG(CD, cd)
dnl Configure needs an empty install.sh file with this, i HATE that...
AC_PROG_INSTALL

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T


dnl Checks for libraries.
dnl Replace `main' with a function in -lc:
dnl AC_CHECK_LIB(c, main)


dnl Checks for header files.
AC_HEADER_STDC


dnl Set some system dependant values.
case "$target" in
  i*-pc-cygwin)
	SHEXT="dll"
	EXEC_SUFFIX=".exe"	
	EXTRA_FLAGS="-DCYGWIN"
	EXTRA_LIBS="-lcygwin -lkernel32"
	TO_BUILD_64=""
	PIPLIB="piplibstatic"
	INSTALL_PIPLIB="install_piplibstatic" ;;
  *)
	SHEXT="so"
	EXEC_SUFFIX=""
	EXTRA_FLAGS=""
	EXTRA_LIBS=""
	PIPLIB="piplibshared piplibstatic"
	INSTALL_PIPLIB="install_piplibshared install_piplibstatic" ;;
esac


dnl Test whether we are using SUN's ld rather than Solaris ld
dnl If so, we need to add -G -Bdynamic to the linking options
dnl This is a hack to avoid having to hack the piplib makefiles
dnl and configure scripts. (by Allen Leung)

SOLARIS_LD=no
case $target_os in
    solaris*)
        case `which ld` in
           /usr/ccs/bin/ld|/usr/ucb/ld)
               SOLARIS_LD=yes
               ;;
           *)
               ld -shared 2>&1 | grep 'fatal: option -h'
               if test "$?" = "0"; then
                   SOLARIS_LD=yes
               fi
               ;;
        esac
        ;;
    *)
        ;;
esac
SOLARIS_LDFLAGS=''
if test "$SOLARIS_LD" = "yes"; then
   SOLARIS_LDFLAGS='-G -Bdynamic'
fi


dnl Checks sizeof the two supported cases.
AC_CHECK_SIZEOF(int,1)
AC_CHECK_SIZEOF(long long int,1)


dnl /**************************************************************************
dnl  *                             Option setting                             *
dnl  **************************************************************************/


dnl The different packages that can be built and installed.
PIP="pip"
INSTALL_PIP="install_pip"
INT_BITS=`expr $ac_cv_sizeof_int \* 8`
INT_DFLAGS=-DLINEAR_VALUE_IS_INT
LONG_BITS=`expr $ac_cv_sizeof_long_long_int \* 8`
LONG_DFLAGS=-DLINEAR_VALUE_IS_LONGLONG
MP_BITS="MP"
MP_DFLAGS=-DLINEAR_VALUE_IS_MP
NEED_MP="no"
GMP_INC="/usr/local/include"
GMP_LIB="/usr/local/lib"


dnl Some default values cause I'm not sure whether autoconf set them, while
dnl documentation says it does...
gmp_package="yes"
gmp_include_package="yes"
gmp_library_package="yes"


dnl Options.
dnl --with-pip=yes, --with-pip=no ou --without-pip
dnl --with-lib=yes, --with-lib=no ou --without-lib
AC_ARG_WITH(pip,
	[  --without-pip           disable the compilation of pip],
	[ echo "Package pip : $withval" &&
	  if test "$withval"="no" ; then 	
		PIP=""
		INSTALL_PIP="" 
	  fi ])

AC_ARG_WITH(lib,
	[  --without-lib           disable the compilation of libs],
	[ echo "Package lib : $withval" &&
	  if test "$withval"="no" ; then 	
		PIPLIB=""
		INSTALL_PIPLIB=""
	  fi ])

AC_ARG_WITH(gmp,
        [  --with-gmp=DIR          DIR where the gmp package is installed],
	[ echo "Package gmp : $withval" &&
	  gmp_package=$withval &&
          GMP_INC=$gmp_package/include &&
          GMP_LIB=$gmp_package/lib &&
          NEED_MP="yes"])

AC_ARG_WITH(gmp_include,
        [  --with-gmp-include=DIR  DIR where gmp.h is installed],
	[ echo "Package gmp-include : $withval" &&
	  gmp_include_package=$withval &&
          GMP_INC=$gmp_include_package &&
	  NEED_MP="yes"])

AC_ARG_WITH(gmp_library,
        [  --with-gmp-library=DIR  DIR where the gmp library is installed],
	[ echo "Package gmp-library : $withval" &&
	  gmp_library_package=$withval &&
          GMP_LIB=$gmp_library_package &&
	  NEED_MP="yes"])

AC_ARG_ENABLE(int-version,
	[  --enable-int-version    Only 'int' version is built],
	[ echo "Package int : $enableval" &&
	  TO_BUILD_32="32" &&
          TO_BUILD_64="" &&
          TO_BUILD_MP=""])

AC_ARG_ENABLE(llint-version,
	[  --enable-llint-version  Only 'long long int' version is built],
	[ echo "Package long long int : $enableval" &&
	  TO_BUILD_32="" &&
          TO_BUILD_64="64" &&
          TO_BUILD_MP=""])

AC_ARG_ENABLE(mp-version,
	[  --enable-mp-version     Only 'MP' version is built],
	[ echo "Package mp : $enableval" &&
	  TO_BUILD_32="" &&
          TO_BUILD_64="" &&
          TO_BUILD_MP="MP" &&
          NEED_MP="yes"])

dnl Packages to build.
PACKAGES="$PIPLIB $PIP"
TO_INSTALL="$INSTALL_PIPLIB $INSTALL_PIP"


dnl /**************************************************************************
dnl  *                            Where is GMP?                               *
dnl  **************************************************************************/


dnl Checking for gmp
AC_MSG_CHECKING(whether gmp works)
if test "$gmp_package" = "no"; then
  echo "GMP package not defined"
  AC_MSG_RESULT(no)
  TO_BUILD_MP=""
else
  if test "$NEED_MP" = "no"; then
    echo "Mode normal GMP"
    TO_BUILD="$TO_BUILD MP"
    AC_CHECK_HEADER(gmp.h,
                    [AC_CHECK_LIB(gmp,
		                  __gmpz_init,
		                  [EXTRA_LIBS="$EXTRA_LIBS -lgmp"],
				  [echo "Can't find gmp library." &&
				   echo "MP version will not be builded." &&
				   TO_BUILD_MP=""])],
		    [echo "Can't find gmp headers." &&
		     echo "MP version will not be builded." &&
		     TO_BUILD_MP=""])
  else
    dnl Default given by --with-X is "yes", --without-X is "no".
    if test "$gmp_package" != "yes" ; then
      echo "(GMP path has been set by user)"
      GMP_DIR=$gmp_package
      GMP_LIB=$GMP_DIR/lib
      GMP_INC=$GMP_DIR/include
      EXTRA_INC="-I$GMP_INC"
      EXTRA_LIBS="$EXTRA_LIBS -L$GMP_LIB"
      dnl Useful for AC_CHECK_X to find what we want.
      CPPFLAGS="-I$GMP_DIR/include $CPPFLAGS"
      LDFLAGS="-L$GMP_DIR/lib $LDFLAGS"
    fi
    
    if test "$gmp_include_package" != "yes" ; then
      CPPFLAGS="-I$GMP_DIR/include $CPPFLAGS"
      EXTRA_INC="-I$GMP_INC"
    fi
    
    if test "$gmp_library_package" != "yes" ; then
      EXTRA_LIBS="$EXTRA_LIBS -L$GMP_LIB"
      LDFLAGS="-L$GMP_DIR/lib $LDFLAGS"
    fi
     
    AC_CHECK_HEADER(gmp.h,
                    [],
                    [AC_MSG_ERROR(Can't find gmp headers.)])
    AC_CHECK_LIB(gmp,
                 __gmpz_init,
                 [EXTRA_LIBS="$EXTRA_LIBS -lgmp"],
                 [AC_MSG_ERROR(Can't find gmp library.)])
    
    AC_MSG_RESULT(yes)
  fi
fi


dnl /**************************************************************************
dnl  *                       Packages to actually build                       *
dnl  **************************************************************************/


TO_BUILD="$TO_BUILD_32 $TO_BUILD_64 $TO_BUILD_MP"


dnl /**************************************************************************
dnl  *                            Substitutions                               *
dnl  **************************************************************************/


dnl Substitutions to do in Makefile.in.
AC_SUBST(VERSION)
AC_SUBST(TO_BUILD)
AC_SUBST(target)
AC_SUBST(CC)
AC_SUBST(LN_S)
AC_SUBST(RANLIB)
AC_SUBST(SHEXT)
AC_SUBST(EXEC_SUFFIX)
AC_SUBST(EXTRA_FLAGS)
AC_SUBST(EXTRA_LIBS)
AC_SUBST(EXTRA_INC)

dnl int/long bits and compilation flags
AC_SUBST(INT_BITS)
AC_SUBST(INT_DFLAGS)
AC_SUBST(LONG_BITS)
AC_SUBST(LONG_DFLAGS)
AC_SUBST(MP_BITS)
AC_SUBST(MP_DFLAGS)

AC_SUBST(prefix)
AC_SUBST(exec_prefix)
AC_SUBST(INSTALL)
AC_SUBST(PACKAGES)
AC_SUBST(TO_INSTALL)

dnl Makefile creation (Allen Leung's fix).
AC_OUTPUT(Makefile:autoconf/Makefile.in)


echo "             /*-----------------------------------------------*"
echo "              *         PIP/PipLib configuration is OK        *"
echo "              *-----------------------------------------------*/"
echo "It appears that your system is OK to start PIP/PipLib compilation. You"
echo "need now to type \"make uninstall\" if your are upgrading an old version."
echo "Next, type \"make\" to build the program. Lastly type \"make install\" to"
echo "install it on your system (log as root if necessary)."
